apply plugin: 'com.android.application'

/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Fetch build number from command line arguments (for CI)
ext.build_number = project.hasProperty('BUILD_NUMBER') ? project.getProperty('BUILD_NUMBER') : "1"
ext.build_label = project.hasProperty('BUILD_LABEL') ? project.getProperty('BUILD_LABEL') : "SNAPSHOT"

// Fetch server details from command line arguments (for CI)
ext.key_server_upload_uri = project.hasProperty('UPLOAD_URI') ? project.getProperty('UPLOAD_URI') : "TODO"
ext.key_server_download_base_uri = project.hasProperty('DOWNLOAD_URI') ? project.getProperty('DOWNLOAD_URI') : "TODO"
ext.virtual_agent_uri =  project.hasProperty('VIRTUAL_AGENT_URI') ? project.getProperty('VIRTUAL_AGENT_URI') : "TODO"
ext.vdh_pin_uri = project.hasProperty('VDH_PIN_URI') ? project.getProperty('VDH_PIN_URI') : "TODO"
ext.attenuation_threshold = project.hasProperty('ATTENUATION_THRESHOLD_IN_SEC') ? project.getProperty('ATTENUATION_THRESHOLD_IN_SEC') : 900
ext.download_schedule = project.hasProperty('DOWNLOAD_SCHEDULE_IN_HOURS') ? project.getProperty('DOWNLOAD_SCHEDULE_IN_HOURS') : 4
ext.logging_enabled = project.hasProperty('LOGGING_ENABLED') ? project.getProperty('LOGGING_ENABLED') : "NO"
ext.verbose_logging = project.hasProperty('VERBOSE_LOGGING') ? project.getProperty('VERBOSE_LOGGING') : "NO"
ext.upload_hostname = project.hasProperty('UPLOAD_HOSTNAME') ? project.getProperty('UPLOAD_HOSTNAME') : "exposure"
ext.download_hostname = project.hasProperty('DOWNLOAD_HOSTNAME') ? project.getProperty('DOWNLOAD_HOSTNAME') : "TODO"
ext.logging_url = project.hasProperty('LOGGING_URL') ? project.getProperty('LOGGING_URL') : "TODO"
ext.logging_hostname = project.hasProperty('LOGGING_HOSTNAME') ? project.getProperty('LOGGING_HOSTNAME') : "TODO"
ext.vdh_hostname = project.hasProperty('VDH_HOSTNAME') ? project.getProperty('VDH_HOSTNAME') : "TODO"


apply plugin: 'com.google.protobuf'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'gov.vdh.exposurenotification'

        minSdkVersion 23
        targetSdkVersion 29
        buildConfigField  "String", "VERBOSE_LOGGING", "\"" + verbose_logging + "\""
        buildConfigField  "String", "LOGGING_ENABLED", "\"" + logging_enabled + "\""
        buildConfigField "int", "DOWNLOAD_SCHEDULE", download_schedule
        buildConfigField "int", "ATTENUATION_THRESHOLD", attenuation_threshold
        buildConfigField "String", "UPLOAD_HOSTNAME", "\"" + upload_hostname + "\""
        buildConfigField "String", "DOWNLOAD_HOSTNAME", "\"" + download_hostname + "\""
        buildConfigField "String", "LOGGING_URL", "\"" + logging_url + "\""
        buildConfigField "String", "LOGGING_HOSTNAME", "\"" + logging_hostname + "\""
        buildConfigField "String", "VDH_HOSTNAME", "\"" + vdh_hostname + "\""

        versionName "1.${build_number}"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true

        resValue("string", "key_server_upload_uri", key_server_upload_uri)
        resValue("string", "key_server_download_base_uri", key_server_download_base_uri)
        resValue("string", "virtual_agent_uri", virtual_agent_uri)
        resValue("string", "vdh_pin_uri", vdh_pin_uri)
        resValue("string", "attenuation_threshold", attenuation_threshold)
        resValue("string", "download_schedule", download_schedule)
        resValue("string", "logging_enabled", logging_enabled)
        resValue("string", "verbose_logging", verbose_logging)

    }

    buildTypes {
        debug {
            versionNameSuffix ".v1.dev"
            minifyEnabled false
            debuggable true
        }

        release {
            resValue "string", "app_version", "${defaultConfig.versionName}"
            minifyEnabled false
        }
    }

    sourceSets {
        debug {
            proto {
                srcDirs 'src/main/java/gov/vdh/exposurenotification/debug/proto'
            }
        }
        release {
            proto {
                srcDir 'src/main/java/gov/vdh/exposurenotification/debug/proto'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    annotationProcessor 'com.google.auto.value:auto-value:1.7'

    debugImplementation 'androidx.fragment:fragment-testing:1.2.4'


    implementation 'androidx.appcompat:appcompat:1.2.0-rc01'
    implementation 'androidx.concurrent:concurrent-futures:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment:1.2.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-guava:2.2.5'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.work:work-runtime:2.3.4'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.gms:play-services-base:17.2.1'
    implementation 'com.google.android.gms:play-services-basement:17.2.1'
    implementation 'com.google.android.gms:play-services-tasks:17.0.2'
    implementation 'com.google.android.gms:play-services-vision:20.0.0'
    implementation 'com.google.android.material:material:1.2.0-beta01'
    implementation 'com.google.auto.value:auto-value-annotations:1.7'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    implementation 'commons-io:commons-io:2.5'

    //noinspection FragmentGradleConfiguration
    testImplementation 'androidx.fragment:fragment-testing:1.2.4'
    testImplementation 'androidx.test.ext:junit:1.1.2-rc01'
    testImplementation 'androidx.test:core:1.3.0-rc01'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.google.guava:guava-testlib:29.0-jre'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}